plugins {
    id "io.freefair.lombok" version "8.0.1"
    id "scala"
}

sourceSets {
    perf {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

configurations {
    perfCompile.extendsFrom compile
    perfRuntime.extendsFrom runtime
    perfImplementation.extendsFrom implementation
}

configurations.configureEach {
    resolutionStrategy {
        force 'com.google.code.gson:gson:2.8.9'
    }
}

dependencies {
    implementation files('libs/tslib-4.6.405-shaded.jar')
    implementation files('libs/openvino-2025.1-x86_64.jar')
    implementation project(':backend:parser')
    implementation project(':backend:common')

    implementation 'it.unimi.dsi:fastutil-core:8.5.12'

    providedDependency 'org.apache.spark:spark-core_2.13:3.3.2'
    providedDependency 'org.apache.spark:spark-sql_2.13:3.3.2'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'com.epam.deltix:gflog-api:3.0.6'
    implementation 'com.epam.deltix:gflog-core:3.0.6'
    runtimeOnly 'com.epam.deltix:gflog-slf4j:3.0.6'
    runtimeOnly 'com.epam.deltix:gflog-jcl:3.0.6'
    implementation 'com.univocity:univocity-parsers:2.9.1'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'commons-io:commons-io:2.15.1'
    implementation 'commons-codec:commons-codec:1.16.0'
    implementation 'ai.djl.huggingface:tokenizers:0.33.0'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
    implementation 'com.knuddels:jtokkit:1.1.0'

    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testImplementation 'org.springframework.security:spring-security-core:6.0.2'
    testImplementation 'org.mockito:mockito-core:5.18.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.18.0'

    perfAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
    perfImplementation 'org.openjdk.jmh:jmh-core:1.37'
}

tasks.named('compileScala') {
    // Scala only needs the declared dependencies
    // (and not longer the output of compileJava)
    classpath = sourceSets.main.compileClasspath
}
tasks.named('compileJava') {
    // Java also depends on the result of Scala compilation
    // (which automatically makes it depend of compileGroovy)
    classpath += files(sourceSets.main.scala.classesDirectory)
}